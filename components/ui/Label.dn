component provides Label requires os.SystemInfo sysInfo, ui.Font {
	
	char text[]
	
	Font font
	
	Color color
	
	Label:Label(char txt[])
		{
		font = new Font(sysInfo.getSystemFont(false), 15)
		
		if (font == null) throw new Exception("System default font could not be loaded")
		
		text = txt
		
		color = new Color(0, 0, 0, 255)
		}
	
	void Label:setFont(Font f)
		{
		font = f
		
		postRepaint()
		}
	
	void Label:setText(char txt[])
		{
		text = txt
		
		postRepaint()
		}
	
	void Label:setColor(Color c)
		{
		color = c
		
		postRepaint()
		}
	
	void Label:setPosition(int x, int y)
		{
		xPosition = x
		yPosition = y
		
		postRepaint()
		}
	
	Point Label:getPosition()
		{
		return new Point(xPosition, yPosition)
		}
	
	void Label:paint(Canvas c)
		{
		c.drawText(new Point2D(xPosition, yPosition, color), font, text)
		}
	
	WH Label:getPreferredSize()
		{
		return new WH(font.getTextWidth(text), font.getFontMetrics().height)
		}
	
	void Label:postRepaint()
		{
		emitevent repaint()
		}
	}
