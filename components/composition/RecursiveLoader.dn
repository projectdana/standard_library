uses Service

data ReqIntf {
	char package[]
	char alias[]
	char semantic[]
	bool isNative
	}

component provides composition.RecursiveLoader requires composition.Search search, Loader loader, NativeLoader nLoader, data.json.JSONParser parser, composition.ObjectWriter, System system, data.IntUtil iu {
	
	ReqIntf[] getRequiredInterfaces(char com[])
		{
		ReqIntf result[]
		
		ObjectWriter reader = new ObjectWriter(com)
		InfoSection section = reader.getInfoSection("DNIL", "json")
		
		JSONElement document = parser.parseDocument(section.content)
		
		JSONElement requiredInterfaces = parser.getValue(document, "requiredInterfaces")
		
		if (requiredInterfaces != null)
			{
			for (int i = 0; i < requiredInterfaces.children.arrayLength; i++)
				{
				JSONElement ri = requiredInterfaces.children[i]
				char package[] = parser.getValue(ri, "package").value
				char alias[] = parser.getValue(ri, "alias").value
				bool isNative = parser.getValue(ri, "native") != null && parser.getValue(ri, "native").value == "true"
				char semantic[]
				if (parser.getValue(ri, "semantic") != null)
					semantic = parser.getValue(ri, "semantic").value
				
				result = new ReqIntf[](result, new ReqIntf(package, alias, semantic, isNative))
				}
			}
		
		return result
		}
	
	bool isIgnore(String interfaces[], char package[])
		{
		for (int i = 0; i < interfaces.arrayLength; i++)
			{
			if (interfaces[i].string == package) return true
			}
		
		return false
		}
	
	bool isAutoBinding(char package[])
		{
		return isIgnore(system.getAutoBindings(), package)
		}
	
	int loadSupportComponent(char path[], LoadedComponents lc)
		{
		for (int i = 0; i < lc.loadedComponents.arrayLength; i++)
			{
			if (lc.loadedComponents[i].path == path)
				return i
			}
		
		IDC res = loader.load(path)
		
		lc.loadedComponents = new LoadedComponent[](lc.loadedComponents, new LoadedComponent(res, path))
		
		return lc.loadedComponents.arrayLength - 1
		}
	
	void resolveDependency(LoadedComponents lc, int comIndex, char package[], char alias[])
		{
		IDC com = lc.loadedComponents[comIndex].class
		
		char np[] = search.getDefaultComponent(package)
		
		if (np == null) throw new Exception("Failed to find default component to satisfy dependency $package")
		
		int index = loadSupportComponent(np, lc)
		IDC ncom = lc.loadedComponents[index].class
		
		if (com.getComponent(alias) == null)
			{
			com.wire(alias, ncom, alias)
			
			if (lc.graph.arrayLength != 0)
				lc.graph = new char[](lc.graph, ",")
			lc.graph = new char[](lc.graph, "$alias:$comIndex:$index")
			
			loadComponents(index, np, lc, null)
			}
		}
	
	bool loadComponents(int comIndex, char path[], LoadedComponents lc, String ignore[])
		{
		//get the required interfaces of path, and load each one (if not already in lc), then wire up the interfaces
		IDC com = lc.loadedComponents[comIndex].class
		ReqIntf ri[] = getRequiredInterfaces(path)
		
		for (int i = 0; i < ri.arrayLength; i++)
			{
			if (!ri[i].isNative && !isAutoBinding(ri[i].package) && !isIgnore(ignore, ri[i].package))
				{
				char pkg[] = ri[i].package
				char alias[] = ri[i].alias
				if (ri[i].semantic.arrayLength != 0)
					{
					pkg = "$pkg:$(ri[i].semantic)"
					alias = "$alias:$(ri[i].semantic)"
					}
				resolveDependency(lc, comIndex, pkg, alias)
				}
				else if (ri[i].isNative && !com.isConnected(ri[i].alias))
				{
				IDC ncom = nLoader.load(ri[i].alias)
				com.wire(ri[i].alias, ncom, ri[i].alias)
				}
			}
		
		//service start
		if (com.hasProvides("Service"))
			{
			Service svc = new Service() from com
			svc.start()
			}
		
		return true
		}
	
	LoadedComponents RecursiveLoader:load(char path[], opt String ignoreIntfs[])
		{
		IDC main = loader.load(path)
		LoadedComponents lc = new LoadedComponents(main, "", new LoadedComponent(main, path))
		
		loadComponents(0, path, lc, ignoreIntfs)
		
		//add components to graph
		char cgraph[]
		for (int i = 0; i < lc.loadedComponents.arrayLength; i++)
			{
			if (cgraph.arrayLength != 0)
				cgraph = new char[](cgraph, ",")
			cgraph = new char[](cgraph, lc.loadedComponents[i].path)
			}
		
		lc.graph = new char[](cgraph, "|", lc.graph)
		
		return lc
		}
	
	}
