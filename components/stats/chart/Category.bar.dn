data Sample {
	dec values[]
	char category[]
	}

//marker length in pixels (the "ticks" on the axes)
const int MARKER_LENGTH = 3

const int MARKER_SIZE = 7

component provides Category:bar requires ChartCore, stats.StatCore stcore, io.Output out, data.IntUtil iu, data.DecUtil du, ui.Font {
	
	Sample samples[]
	
	Color seriesColor = new Color(100, 200, 200, 255)
	Color stdDevColor = new Color(0, 0, 0, 255)
	Color axisColor = new Color(0, 0, 0, 255)
	Color gridColor = new Color(220, 220, 220, 255)
	Color bgColor = new Color(255, 255, 255, 255)
	
	Font axisFont
	Font labelFont
	
	dec highestX
	dec lowestX
	dec highestY
	dec lowestY
	
	dec numberIntervalX = 1.0
	dec markerIntervalX = 1.0
	
	dec numberIntervalY
	dec markerIntervalY
	dec gridIntervalY
	
	dec xAxMax
	dec xAxMin
	dec yAxMax
	dec yAxMin
	
	//bar width, in percentage of category area
	dec barWidth = 50.0
	int barPad = 0
	
	bool showStdDev
	
	Category:Category()
		{
		super()
		
		axisFont = new Font("SourceSansPro.ttf", 12)
		
		setXMarkerInterval(1.0)
		}
	
	void Category:setAxisFont(Font f)
		{
		axisFont = f
		super(f)
		}
	
	void Category:addSample(char cat[], dec yvalues[])
		{
		//update our highest and lowest X and Y points, to use in normalising coordinates
		dec hy = stcore.max(yvalues)
		dec ly = stcore.min(yvalues)
		
		xAxMax += 1.0
		
		if (samples == null)
			{
			highestY = hy
			lowestY = ly
			}
			else
			{
			if (hy > highestY) highestY = hy
			if (ly < lowestY) lowestY = ly
			}
		
		//add sample
		samples = new Sample[](samples, new Sample(yvalues, cat))
		
		// -- automated calculation of the highest and lowest points on the axis --
		
		//update the axis endpoints, if the series values are now outside the bounds of whatever endpoints have been set
		if (highestY > yAxMax) yAxMax = highestY
		if (lowestY < yAxMin) yAxMin = lowestY
		
		setXMinMax(xAxMin, xAxMax)
		setYMinMax(yAxMin, yAxMax)
		
		postRepaint()
		}
	
	void Category:setYLabelInterval(dec ivr)
		{
		numberIntervalY = ivr
		super(ivr)
		}
	
	void Category:setYMinMax(dec min, dec max)
		{
		//TODO: disallow values that can't contain all of the graph points...
		yAxMin = min
		yAxMax = max
		
		super(min, max)
		}
	
	void Category:setSeriesColor(Color c)
		{
		seriesColor = c
		}
	
	void Category:showErrorBars(bool on)
		{
		showStdDev = on
		}
	
	void Category:clampErrorBars(dec low, dec high)
		{
		
		}
	
	void Category:setCatDisplayWidth(dec percent)
		{
		if (percent <= 0.0 || percent > 100.0)
			throw new Exception("display width must be a percentage, between 1 and 100")
		
		barWidth = percent
		}
	
	void Category:setCatDisplayPadding(int pixels)
		{
		barPad = pixels
		}
	
	// -- custom rendering --
	
	void Category:drawToCanvas(Canvas c)
		{
		paint(c)
		}
	
	void prepAxisLimits()
		{
		dec highX
		dec lowX
		dec highY
		dec lowY
		
		//calculate what our exact min/max should be, and account for stdDev
		for (int i = 0; i < samples.arrayLength; i++)
			{
			dec mean = stcore.mean(samples[i].values)
			
			if (showStdDev)
				{
				dec stdDev = stcore.stdDev(samples[i].values)
				
				if (mean - stdDev < lowY) lowY = mean - stdDev
				if (mean + stdDev > highY) highY = mean + stdDev
				}
				else
				{
				if (mean < lowY) lowY = mean
				if (mean > highY) highY = mean
				}
			
			highX += 1.0
			}
		
		//expand axes if needed, otherwise respect user-defined limits
		
		if (highX > xAxMax) xAxMax = highX
		if (lowX < xAxMin) xAxMin = lowX
		
		if (highY > yAxMax) yAxMax = highY
		if (lowY < yAxMin) yAxMin = lowY
		
		setXMinMax(xAxMin, xAxMax)
		setYMinMax(yAxMin, yAxMax)
		}
	
	// -- screen rendering (temporary?) --
	
	void Category:paint(Canvas c)
		{
		prepAxisLimits()
		
		Point pos = getPosition()
		WH wh = getSize()
		
		c.pushSurface(new Rect(pos.x, pos.y, wh.width, wh.height), 0, 0, 255)
		
		preparePlotArea()
		
		//background
		c.drawRect(new Rect2D(0, 0, wh.width, wh.height, bgColor))
		
		//grid lines, if any
		drawGrid(c)
		
		//data
		
		int xSpacing = (getPlotPoint(1.0, 0.0)[0] - getPlotPoint(0.0, 0.0)[0]+1) - (barPad * 2)
		int barWidthPX = (barWidth / 100.0) * xSpacing
		
		dec thisX = 0.0
		for (int i = 0; i < samples.arrayLength; i++)
			{
			//this kind of graph plots the mean of the set
			dec thisY = stcore.mean(samples[i].values)
			
			//top-left
			int xy[] = getPlotPoint(thisX, thisY)
			
			//top-right
			int xyTR[] = getPlotPoint(thisX+1.0, thisY)
			
			//bottom-left
			int xyBL[] = getPlotPoint(thisX, 0.0)
			
			int yHigh = xy[1]
			if (xy[1] > xyBL[1])
				yHigh = xyBL[1]
			int yLow = xyBL[1]
			if (xy[1] > xyBL[1])
				yLow = xy[1]
			
			int centreX = xy[0] + ((xyTR[0] - xy[0]) / 2)
			
			c.drawRect(new Rect2D((centreX - (barWidthPX / 2)) + barPad, yHigh, barWidthPX, yLow - yHigh, seriesColor))
			
			if (showStdDev)
				{
				dec stdDev = stcore.stdDev(samples[i].values)
				
				if (stdDev != 0.0)
					{
					int xyHigh[] = getPlotPoint(thisX, thisY+stdDev)
					int xyLow[] = getPlotPoint(thisX, thisY-stdDev)
					
					c.drawLine(new Line2D(centreX, xyHigh[1], centreX, xyLow[1], stdDevColor))
					
					c.drawLine(new Line2D(centreX-MARKER_SIZE/2, xyHigh[1], centreX+MARKER_SIZE/2, xyHigh[1], stdDevColor))
					c.drawLine(new Line2D(centreX-MARKER_SIZE/2, xyLow[1], centreX+MARKER_SIZE/2, xyLow[1], stdDevColor))
					}
				}
			
			//category label
			int textWidth = axisFont.getTextWidth(samples[i].category)
			
			c.drawText(new Point2D(centreX - (textWidth / 2), getPlotArea().y + getPlotArea().height + MARKER_LENGTH, axisColor), axisFont, samples[i].category)
			
			thisX += 1.0
			}
		
		//axes and labels
		drawAxes(c)
		
		c.popSurface()
		}
	
	}