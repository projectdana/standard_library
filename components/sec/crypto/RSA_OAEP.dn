uses data.String

interface CipherLib {
	int aes_cbc_encryptInit(int keyLength, char key[], char iv[])
	byte[] aes_cbc_encryptPart(int handle, byte content[])
	byte[] aes_cbc_encryptFinish(int handle)
	bool aes_cbc_encryptOK(int handle)
	
	int aes_cbc_decryptInit(int keyLength, char key[], char iv[])
	byte[] aes_cbc_decryptPart(int handle, byte content[])
	byte[] aes_cbc_decryptFinish(int handle)
	bool aes_cbc_decryptOK(int handle)
	
	int aes_gcm_encryptInit(int keyLength, char key[], char iv[])
	bool aes_gcm_encryptAAD(int handle, byte content[])
	byte[] aes_gcm_encryptPart(int handle, byte content[])
	byte[] aes_gcm_encryptFinish(int handle)
	bool aes_gcm_encryptOK(int handle)
	byte[] aes_gcm_encryptGetTag(int handle)
	
	int aes_gcm_decryptInit(int keyLength, char key[], char iv[])
	bool aes_gcm_decryptAAD(int handle, byte content[])
	byte[] aes_gcm_decryptPart(int handle, byte content[])
	byte[] aes_gcm_decryptFinish(int handle)
	void aes_gcm_decryptSetTag(int handle, byte tag[])
	bool aes_gcm_decryptOK(int handle)
	
	void instance_cleanup(int handle)
	
	int rsa_oaep_init(char key_pem[], byte mode)
	byte[] rsa_oaep_encrypt(int handle, byte content[])
	byte[] rsa_oaep_decrypt(int handle, byte content[])
	void rsa_oaep_cleanup(int handle)
	
	int rsa_pss_init(char key_pem[], byte mode)
	byte[] rsa_pss_sign(int handle, byte content[])
	bool rsa_pss_verify(int handle, byte plain[], byte signed[])
	void rsa_pss_cleanup(int handle)
	
	bool rsa_generate_key(int keyBits, String public, String private)
	bool rsa_convert_key(char public[], char private[], byte type, String newPublic, String newPrivate, byte newType)
	}

component provides RSA_OAEP(Destructor) requires native CipherLib lib {
	
	int platformHandle
	int myMode
	
	RSA_OAEP:RSA_OAEP(char key_pem[], byte mode)
		{
		platformHandle = lib.rsa_oaep_init(key_pem, mode)
		
		if (platformHandle == 0) throw new Exception("failed to create OAEP instance based on given key")
		
		myMode = mode
		}
	
	//use public key to encrypt something
	byte[] RSA_OAEP:encrypt(byte plainText[])
		{
		return lib.rsa_oaep_encrypt(platformHandle, plainText)
		}
	
	//use private key to decrypt something
	byte[] RSA_OAEP:decrypt(byte ciphertext[])
		{
		return lib.rsa_oaep_decrypt(platformHandle, ciphertext)
		}
	
	void Destructor:destroy()
		{
		if (platformHandle != 0)
			lib.rsa_oaep_cleanup(platformHandle)
		}
	
	}